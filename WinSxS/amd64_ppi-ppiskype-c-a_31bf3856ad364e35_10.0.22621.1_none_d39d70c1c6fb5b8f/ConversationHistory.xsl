<?xml version="1.0"?>
<xsl:stylesheet
    version="1.0"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:msxsl="urn:schemas-microsoft-com:xslt"
    xmlns ="http://schemas.microsoft.com/2008/10/sip/convItems"
    xmlns:history="http://schemas.microsoft.com/2008/10/sip/convItems"
    xmlns:rtc="urn:microsoft-rtc-xslt-functions"
    >
    <!--
      This transform transforms the aggregated convlog items in Xml into 
      conversation history email body in html format

      Author: dejunz
        Date: 02/07/08
    -->
    <xsl:output method="html" encoding="utf-8" indent="yes"/>

    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
    <!-- +                                                                  +-->
    <!-- +  Root Document Templates                                         +-->
    <!-- +                                                                  +-->
    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="/">
        <xsl:element name="html">
            <xsl:if test="./history:conversations/history:conversationInfo/@readingOrder">
                <xsl:attribute name="dir">
                    <xsl:value-of select="./history:conversations/history:conversationInfo/@readingOrder"/>
                </xsl:attribute>
            </xsl:if>
            <xsl:comment>
                Document generated by Office Communicator conversation history archiver
            </xsl:comment>
            <head>
                <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
                <style type="text/css">
                    <xsl:comment>
                        @charset "utf-8";
                        body {
                        margin-left: 11px;
                        margin-top: 11px;
                        margin-right: 11px;
                        margin-bottom: 11px;
                        }

                        p {
                        margin:0;
                        padding:0;
                        }

                        .header {
                        font-size: 16px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: bold;
                        font-variant: normal;
                        text-transform: none;
                        color: #000000;
                        text-decoration: none;
                        font-family: "Segoe UI";
                        }

                        .callDuration {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #000000;
                        text-decoration: none;
                        }

                        .notes_bkg {
                        background-color: #e4eefc;
                        border-bottom-width: 1px;
                        border-bottom-style: solid;
                        border-bottom-color: #b5c4df;
                        vertical-align: middle;
                        padding-top: 5px;
                        padding-right: 8px;
                        padding-bottom: 5px;
                        padding-left: 20px;
                        }
                        .notes_title {
                        font-family: "Segoe UI";
                        font-size: 12px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: bold;
                        font-variant: normal;
                        text-transform: none;
                        color: #15429d;
                        text-decoration: none;
                        }

                        .note_message {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #15429d;
                        text-decoration: none;
                        }

                        .list_name {
                        font-family: "Segoe UI";
                        font-size: 12px;
                        font-style: normal;
                        font-weight: bold;
                        font-variant: normal;
                        text-transform: none;
                        color: #000000;
                        text-decoration: none;
                        line-height: 18px;
                        }

                        .list_title_company {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: uppercase;
                        color: #666666;
                        text-decoration: none;
                        }

                        .list_label {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 25px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #C0C0C0;
                        text-decoration: none;
                        }

                        .links {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #0000FF;
                        text-decoration: underline;
                        }

                        .line {
                        border-top-width: 1px;
                        border-top-style: solid;
                        border-top-color: #b5c4df;
                        height: 1px;
                        background-color: #b5c4df;
                        }
                        .footer_line {
                        padding-top: 30px;
                        padding-bottom: 0px;
                        padding-right: 0px;
                        padding-left: 0px;
                        }
                        .footer {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 0px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #C0C0C0;
                        text-decoration: none;
                        padding-top: 11px;
                        padding-bottom: 8px;
                        }

                        .header_box {
                        padding-top: 11px;
                        padding-bottom: 11px;
                        }

                        .contacts_box {
                        padding-top: 20px;
                        padding-bottom: 0px;
                        padding-right: 0px;
                        padding-left: 80px;
                        }

                        .list_value {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 25px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #000000;
                        text-decoration: none;
                        }

                        .im_sender {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: bold;
                        font-variant: normal;
                        text-transform: none;
                        color: #666666;
                        text-decoration: none;
                        }

                        .message_timestamp {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: bold;
                        font-variant: normal;
                        text-transform: none;
                        color: #666666;
                        text-decoration: none;
                        }
                        .messageInfo {
                        margin-left:8px;
                        }
                        .notification {
                        font-family: "Segoe UI";
                        font-size: 11px;
                        font-style: normal;
                        line-height: 18px;
                        font-weight: normal;
                        font-variant: normal;
                        text-transform: none;
                        color: #666666;
                        text-decoration: none;
                        }
                    </xsl:comment>
                </style>
            </head>
            <xsl:element name="body">
                <xsl:if test="./history:conversations/history:conversationInfo/@readingOrder">
                    <xsl:attribute name="dir">
                        <xsl:value-of select="./history:conversations/history:conversationInfo/@readingOrder"/>
                    </xsl:attribute>
                </xsl:if>
                <xsl:apply-templates/>
            </xsl:element>
        </xsl:element>
    </xsl:template>

    <xsl:template match="history:conversationInfo">
        <!--Conversation summary and Participant contact info-->
        <xsl:if test="@showParticipantInfo">
            <xsl:call-template name="templateCallLog"/>
        </xsl:if>
    </xsl:template>
    <xsl:template match="history:conversationXml">
        <!-- Conversation contents, Notifications, ErrorMessages, Context-->
        <xsl:if test="not(../history:conversationInfo/@showParticipantInfo)">
            <xsl:apply-templates select="history:convNotification |
                                       history:voiceEvent |
                                       history:imReceived | 
                                       history:convErrorMessage | 
                                       history:previousConversationContent |
                                       history:fileTransferInstance |
                                       history:convContextAdded"/>
        </xsl:if>

    </xsl:template>

    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
    <!-- +                                                                  +-->
    <!-- +  Header- Conversation Summary and ParticipantInfo for Call Logs  +-->
    <!-- +                                                                  +-->
    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
    <xsl:template name="templateCallLog">
        <table width="680px" border="0" cellpadding="0" cellspacing="2">
            <tr>
                <td colspan="2" class="header_box">
                    <p >
                        <span class="header">
                            <xsl:value-of select="history:summary" disable-output-escaping="yes"/>
                        </span>
                        <br />
                        <xsl:if test ="history:duration">
                            <span class="callDuration">
                                <xsl:value-of select ="history:duration"  disable-output-escaping="yes"/>
                            </span>
                        </xsl:if>
                    </p>
                </td>
            </tr>

            <xsl:for-each select="../history:conversationXml/history:convContextAdded">
                <tr>
                    <td colspan="2" valign="middle" class="notes_bkg">
                        <p>
                            <span class="notes_title">
                                <xsl:value-of select="@fromDisplay"/>
                            </span>
                            <br />
                            <span class="note_message">
                                <xsl:value-of select="@message" disable-output-escaping="yes"/>
                            </span>
                        </p>
                    </td>
                </tr>
            </xsl:for-each >

            <tr>
                <td colspan="2" align="left" valign="top">
                    <table class="contacts_box">
                        <!-- contact infos-->
                        <xsl:for-each select="./history:participantInfo">
                            <xsl:if test="@islocal='false' and string(@isGateway)!='true' and string(@isCallParkService)!='true'">
                                <xsl:call-template name ="history:participantInfo"/>
                            </xsl:if>
                        </xsl:for-each>
                    </table>
                </td>
            </tr>
            <tr>
                <td colspan="2" class="footer_line">
                    <table border="0" cellspacing="0" cellpadding="0" height="1">
                        <td height="1" width="400" class="line" ></td>
                        <td></td>
                    </table>
                </td>
            </tr>
            <tr>
                <td colspan="2" class="footer">
                    <xsl:value-of select="./@productName" disable-output-escaping="yes"/>
                </td>
            </tr>
        </table>

    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template name="history:participantInfo">
        <tr>
            <td>
                <table border="0" cellspacing="0" cellpadding="0">
                   <!--display name-->
                   <xsl:if test ="string(history:contactInfo/@displayName)">
                        <tr>
                            <td colspan="2">
                                <span class="list_name">
                                    <xsl:value-of select="history:contactInfo/@displayName" disable-output-escaping="yes"/>
                                </span>
                                <br />
                                <xsl:if test ="string(history:contactInfo/@title) or string(history:contactInfo/@company)">
                                    <span class="list_title_company">
                                        <!--title, company-->
                                        <xsl:value-of select="history:contactInfo/@title" disable-output-escaping="yes"/>
                                        <xsl:if test ="string(history:contactInfo/@title) and string(history:contactInfo/@company)">
                                            <xsl:text> , </xsl:text>
                                        </xsl:if>
                                        <xsl:value-of select="history:contactInfo/@company" disable-output-escaping="yes"/>                                        
                                    </span>
                                </xsl:if>
                            </td>
                        </tr>
                    </xsl:if>

                    <!-- phone -->
                    <xsl:for-each select="history:contactInfo/history:phone">
                        <tr>
                            <td width="55" valign="middle">
                                <span class="list_label">
                                    <xsl:value-of select="@deviceType" disable-output-escaping="yes"/>
                                </span>
                            </td>
                            <td valign="middle">
                                <span class="list_label">
                                    <a dir="ltr" style="color: blue; text-decoration: underline; text-underline: single" href="{@uri}">
                                        <xsl:value-of select="@displayString" disable-output-escaping="yes"/>
                                    </a>
                                    <br />
                                </span>
                            </td>
                        </tr>

                    </xsl:for-each>
                    <!--EmailAddress-->
                    <xsl:if test ="string(history:contactInfo/@emailAddress)">
                        <tr>
                            <td width="55" valign="middle">
                                <span class="list_label">
                                    <xsl:value-of select="../../history:conversationInfo/history:resourceTable/history:localizedEmailLabel"
                                                      disable-output-escaping="yes"/>
                                </span>
                            </td>
                            <td valign="middle">
                                <span class="list_label">
                                    <a dir="ltr" style="color: blue; text-decoration: underline; text-underline: single"
                                            href="mailto:{history:contactInfo/@emailAddress}">
                                        <xsl:value-of select="history:contactInfo/@emailAddress" disable-output-escaping="yes"/>
                                    </a>
                                    <br />
                                </span>

                            </td>
                        </tr>
                    </xsl:if>
                    <!--Im Address-->
                    <xsl:if test ="string(history:contactInfo/@imAddress)">
                        <tr>
                            <td width="55" valign="middle">
                                <span class="list_label">
                                    <xsl:value-of select="../../history:conversationInfo/history:resourceTable/history:localizedImLabel"
                                                      disable-output-escaping="yes"/>
                                </span>
                            </td>
                            <td valign="middle">
                                <span class="list_label">
                                    <a dir="ltr" style="color: blue; text-decoration: underline; text-underline: single"
                                            href="sip:{history:contactInfo/@imAddress}">
                                        <xsl:value-of select="history:contactInfo/@imAddress" disable-output-escaping="yes"/>
                                    </a>
                                    <br />
                                </span>
                            </td>
                        </tr>
                    </xsl:if>
                </table>
            </td>
        </tr>
    </xsl:template>


    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
    <!-- +                                                                  +-->
    <!-- +  Conversation contents, Notifications, ErrorMessages, Context    +-->
    <!-- +                                                                  +-->
    <!-- +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:previousConversationContent">
        <xsl:value-of select="." disable-output-escaping="yes"/>
    </xsl:template>

    <xsl:template match="history:imReceived">
        <div>
            <xsl:if test="string(history:messageInfo)">
                <xsl:if test="not(@chunked)">
                    <span class ="im_sender">
                        <xsl:value-of select="@displayName"/>
                    </span>
                    <xsl:text> </xsl:text>
                    <span class ="message_timestamp">
                        <xsl:value-of select="@ts"/>:
                    </span>
                </xsl:if>
                <xsl:apply-templates/>
            </xsl:if>
        </div>
    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:voiceEvent">
        <div>
            <span class ="message_timestamp">
                <xsl:value-of select="@ts"/>
            </span>
            <span class ="notification">
                <xsl:text> </xsl:text>
                <xsl:value-of select="@msgString" disable-output-escaping="yes"/>
            </span>
        </div>
    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:convContextAdded">
        <div class="notes_bkg">
            <span class="notes_title">
                <xsl:value-of select="@fromDisplay"/>
            </span>
            <br />
            <span class="note_message">
                <xsl:value-of select="@message" disable-output-escaping="yes"/>
            </span>
        </div>
    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:convNotification">
        <xsl:if test="normalize-space(@msgString) != ''">
            <div>
                <span class ="message_timestamp">
                    <xsl:value-of select="@ts"/>
                </span>
                <span class ="notification">
                    <xsl:text> </xsl:text>
                    <xsl:value-of select="@msgString" disable-output-escaping="yes"/>
                </span>
            </div>
        </xsl:if>
    </xsl:template>


    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:fileTransferInstance">
        <div>
            <span class ="message_timestamp">
                <xsl:value-of select="@ts"/>
            </span>
            <span class ="notification">
                <xsl:text> </xsl:text>
                <xsl:value-of select="@inviteMessage" disable-output-escaping="yes"/>
            </span>
        </div>
    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

    <xsl:template match="history:messageInfo">
        <div class="messageInfo">
            <xsl:choose>
                <xsl:when test="@type='application/x-ms-ink'">
                    <object classid="clsid:BFDF5491-B2DA-4426-9E49-0AA02C9DCAD4">
                        <xsl:element name="param">
                            <xsl:attribute name="name">Content</xsl:attribute>
                            <xsl:attribute name="value">
                                <xsl:value-of select="."/>
                            </xsl:attribute>
                        </xsl:element>
                    </object>
                </xsl:when>
                <xsl:when test="@type='image/gif'">
                    <object classid="clsid:BFDF5491-B2DA-4426-9E49-0AA02C9DCAD4">
                        <xsl:element name="param">
                            <xsl:attribute name="name">Content</xsl:attribute>
                            <xsl:attribute name="value">
                                <xsl:value-of select="." />
                            </xsl:attribute>
                        </xsl:element>
                    </object>
                    <span class="notification">
                        <xsl:value-of select="../../../history:conversationInfo/history:resourceTable/history:localizedInkLabel"
                                          disable-output-escaping="yes"/>
                    </span>
                </xsl:when>
                <xsl:otherwise>
                    <xsl:value-of select="." disable-output-escaping="yes"/>
                </xsl:otherwise>
            </xsl:choose>
        </div>
    </xsl:template>

    <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
    <xsl:template match="history:convErrorMessage">
        <xsl:if test="normalize-space(@msgString) != ''">
            <div>
                <span class ="message_timestamp">
                    <xsl:value-of select="@ts"/>
                </span>
                <span class ="notification">
                    <xsl:text> </xsl:text>
                    <xsl:value-of select="@msgString" disable-output-escaping="yes"/>

                    <xsl:if test="history:messageInfo">
                        <xsl:apply-templates select="history:messageInfo" />
                    </xsl:if>
                </span>
            </div>
        </xsl:if>
    </xsl:template>

    <!--Ignore the following events-->
    <xsl:template  match="history:conversationProperty">
        <xsl:apply-templates select="blank"/>
    </xsl:template>
    <xsl:template  match="history:representationInfo">
        <xsl:apply-templates select="blank"/>
    </xsl:template>
</xsl:stylesheet>